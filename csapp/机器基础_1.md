
###  Object Code

Object code 是 .o 文件， 里面基本是每个操作的 binary encoding. 加上链接即可变成执行文件。 跟 Assembly 是意义对应的。

通过 objdump -d <exec> 或者 gdb 来获得 object code 以及对应的 Assmeble code.

这样能得到一些函数做 Reverse Engineering。

### Register

|63 | 31 | 15 | 7 0 | 作用|
|---|----|----|---|------|
|%rax|%eax|%ax|%al|返回值|
|%rbx|%ebx|%bx|%bl|callee saved|
|%rcx|%ecx|%cx|%cl|4th argument|
|%rdx|%edx|%dx|%dl|3rd argument|
|%rsi|%esi|%si|%sil|2nd argument|
|%rdi|%edi|%di|%dil|1st argument|
|%rbp|%ebp|%bp|%bpl|callee saved|
|%rsp|%esp|%sp|%spl| stack pointer|
|%r8 |%r8d|%r8w|%r8b| 5th argument|
|%r9 |%r9d|%r9w|%r9b| 6th argument|
|%r10 |%r10d|%r10w|%r10b| caller saved|
|%r11 |%r11d|%r11w|%r11b| caller saved|
|%r12 |%r12d|%r12w|%r12b| callee saved|
|%r13 |%r13d|%r13w|%r13b| callee saved|
|%r14 |%r14d|%r14w|%r14b| callee saved|
|%r15 |%r15d|%r15w|%r15b| callee saved|



registers: 寄存器，registers 既不是 memory 也不是 cache，是 cpu 中的特殊构成

然后这里都可以 reference 低位


program counter: address of next instruction, 用来存下一条指令地址，在 x86-64 中也叫 'rip' 


### Memory Addressing modes

注意 (%rax) 和 %rax 的区别, 可以看这个问题：

[What is the difference between “mov (%rax),%eax” and “mov %rax,%eax”?](https://stackoverflow.com/questions/41232333/what-is-the-difference-between-mov-rax-eax-and-mov-rax-eax)


加了括号是 deference， 是去读 rax 中存的内存地址，然后再去这个内存地址中读值。

查字典：

```
dereference |dēˈrefərəns|
verb [with object] Computing
obtain from (a pointer) the address of a data item held in another location.
```


```
mov (%rax), %eax                
```


> dereferences the memory address stored in rax, reads a 32-bit value from that memory address, and stores it in the eax register.




之所以这里强调 32-bit 是因为这里是移到 %eax.

而没有加括号则是直接读寄存器中的值。



为了更明白这个区别，可以做 CSAPP 书中练习 3.1 


地址  | 值
------|----
0x100 | 0xFF
0x104 | 0xAB
0x108 | 0x13
0x10C | 0x11



寄存器 | 值
------|----
%rax  | 0x100
%rcx  | 0x1
%rdx  | 0x3


填表：

操作数 | 值 | 解答
------|----|-----
%rax  | 0x100| 直接读取寄存器中的值
0x104 | 0xAB | 内存0x104处的值
$0x108| 0x108| 常数
(%rax)| 0xFF | 去读内存地址0x100中的值0xFF
4(%rax)| 0xAB| (%rax + 4) = (0x104) 去取地址 0x104中的值
9(%rax,%rdx)|0x11| (%rdx+%rax+9) = (0x3+0x100+0x9) = (0x10C) 取值 0x11
260(%rcx, %rdx) | 0x13 | (260+%rcx+%rdx) = (0x104+0x1+0x1) 取值 0x13
0xFC(, %rcx, 4) | 0xFF | (0xFC + 4 * 0x1) = (0x100)
(%rax, %rdx, 4) | 0x11 | (0x100 + 4 * 0x3) = (0x10C) 取值0x11  

正如[slides-05-machine-basics](http://www.cs.cmu.edu/afs/cs/academic/class/15213-f15/www/lectures/05-machine-basics.pdf) 上写的：

对于 Memory addressing modes 我们有两种：

- Normal (R) 去取 Mem[Reg[R]]
- Displacement D(R) Mem[Reg[R]+D]

最通用的还是 D(Rb, Ri, S) 是 Mem[Reg[Rb]+S*Reg[Ri]+ D]

还有例子：

```
void swap(long *xp, long *yp)
{
  long t0 = *xp;
  long t1 = *yp;
  *xp = t1;
  *yp = t0;
}
```

对应的 assemble code：

```
swap:
  movq (%rdi), %rax
  movq (%rsi), %rdx
  movq %rdx, (%rdi)
  movq %rax, (%rsi)
  ret
```


这里可以注意的是 long *xp 是第一个参数，所以我们 %rdi 对应它，但是因为是指针格式，取地址，所以我们用的是 (%rdi), 取第一个参数的地址。这里需要记住。


再看一个例子，是[slides-07-machine-procedures](http://www.cs.cmu.edu/afs/cs/academic/class/15213-f15/www/lectures/07-machine-procedures.pdf) 中出现的：

在 data flow example 中：

```
void multstore(long x, long y, long *dest)
{
  long t = mult2(x, y);
  *dest = t;
}
```

ASM 

```
0000000...4005440 <multstore>
  # x in %rdi, y in %rsi, dest in %rdx  ...
  400541: mov      %rdx, %rbx       #save dest 
  400544: callq    400550 <mult2>   #mult2(x,y)
  # t in %rax
  400549: mov      %rax,(%rbx)      #Save at dest ••• 
  ...
```

所以这里写的是 dest in %rdx， not *dest. interesting.




所以 slides 里面也强调这个 (R)  Mem[Reg[R]] 像 pointer dereferencing. 


还可以参看[slides-Recitation-04](http://www.cs.cmu.edu/afs/cs/academic/class/15213-f15/www/recitations/rec04.pdf) 中的例子：

| instruction | effect |
|-------------|--------|
|mov %rbx, %rdx| rdx = rbx |
|add (%rdx), %r8 | r8 += value at rdx(here we read from address in regster rdx)|
|mul $3, %r8| r8 *=3 |
|sub $1, %r8| r8--   |
|lea (%rdx, %rbx,2), %rdx | rdx = rbx * 2 + rdx(doesn't dereference) |


### leaq 

leaq 做的事情就仅仅是计算内存而已，比如：

- leaq a(b, c, d), %rax

先计算地址a + b + c * d，然后把最终地址载到寄存器rax中。


> lea是“load effective address”的缩写，简单的说，lea指令可以用来将一个内存地址直接赋给目的操作数，
>
> 例如：lea eax,[ebx+8]就是将ebx+8这个值直接赋给eax，而不是把ebx+8处的内存地址里的数据赋给eax。
> 而mov指令则恰恰相反，例如：mov eax,[ebx+8]则是把内存地址为ebx+8处的数据赋给eax。


参见：

[汇编总结:lea指令](https://my.oschina.net/guonaihong/blog/508907)


针对 leaq 有 trick 是用它来做计算：

比如例子:

```
long m12(long x)
{
  return x * 12;
}
```

ASM

```
leaq (%rdi, %rdi, 2), %rax # t <- x + x*2
salq $2, %rax              # return t << 2
```



### Condition Codes


- CF: carry flag (unsigned)
- SF: sign flag(signed)
- ZF: zero flag 
- OF: overflow flag (signed)

注意这些顺序

- compq b,a 就像计算 a - b 但是不把值放入 destination.
- testq b,a 计算 a & b


例子

```
int gt(long x, long y)
{
  return x > y;
}
```


```
# rdi - x, rsi - y, rax - return value
cmpq %rsi, %rdi  # compare x : y
setg %al         # set when > 
movzbl %al, %eax # zero rest of %rax
ret
```

### Conditional branches

记住 jmp/je/jne/js/jns/jg/jge/jl/jle/ja/jb 这几个对应的后缀。

例子：

```
long absdiff(long x, long y)
{
  long result;
  if (x > y)
    result = x - y;
  else
    result = y - x;
  return result;
}
```

ASM 

```
absdiff:
  cmpq %rsi, %rdi #x,y
  jle .L4
  movq %rdi, %rax
  sub %rsi, %rax
  ret
.L4:     # x <= y
  movq %rsi, %rax
  subq %rdi, %rax
  ret
```


### Loops & Switch

这部分以及以上的更多参见[slides-06-machine-contro](http://www.cs.cmu.edu/afs/cs/academic/class/15213-f15/www/lectures/06-machine-control.pdf)



 